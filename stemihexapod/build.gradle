apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.1.2"

ext {
    bintrayRepo = 'STEMI'
    bintrayName = 'stemi-hexapod'
    userOrganization = 'marioloncar'

    publishedGroupId = 'com.stemi.education'
    artifact = 'stemi-hexapod'
    libraryVersion = version

    libraryName = 'STEMIHexapod'

    developerId = 'marioloncar'
    developerName = 'Mario Lonƒçar'
    developerEmail = 'loncar.mario1@gmail.com'
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 4
        versionName version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'STEMI'
        name = 'stemi-hexapod'
        userOrg = 'marioloncar'
        desc = 'Android library for easy STEMI Hexapod app programming'
        websiteUrl = 'https://github.com/marioloncar/STEMI_Hexapod'
        vcsUrl = 'https://github.com/marioloncar/STEMI_Hexapod.git'
        licenses = ["MIT"]
        publish = true
        publicDownloadNumbers = false
        version {
            desc = 'Now uses connection status in calibration mode'
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}
